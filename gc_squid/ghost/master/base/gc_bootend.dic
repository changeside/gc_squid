

//------------------------------------------------------------------------------
//  Booting sequences
//------------------------------------------------------------------------------
CheckInputs {
	_fb = ""
	//Check if firstboot inputs were disrupted
	if username == ""; _fb = "OnUsernameSet"
	elseif ARRAYSIZE(userbirthday) != 2; _fb = "OnUserBirthday"
	elseif userspecies == ""; _fb = "OnUserSpecies"
	elseif userplanet == ""; _fb = "OnUserPlanet"

	//If firstboot was disrupted break OnBoot and go to the firstboot
	if _fb[0] != ""; OnBoot_Firstbootdisrupted(_fb)
	else; ""
}
OnBoot
{
	"\![updatebymyself,checkonly]"
	--
	OnRender
	--
	if !DAY(squidbirthday); birthdaycheck[0] = 0
	if !DAY(userbirthday); birthdaycheck[1] = 0
	--
	_fb = CheckInputs
	--
	if _fb == ""; OnBoot_Dialogue
	else; _fb
}
OnBoot_Firstbootdisrupted
{
	case _argv[0] {
		when "OnUsernameSet";
			"%(b0)There seems to have been some misunderstanding between us.\x\c%(b0)When I asked you what your name was, I was intending to receive an answer. Not for you to send me back so you can go off and do.\w4.\w4.\w4 whatever it is that you do.\x\c%(b0)I might know what you do when you're not here if you had given us the chance to get to know each other instead of being so disgustingly rude.\x\c%(b0)So, let's try this again. What's your name?"
		when "OnUserBirthday";
			"%(b0)If you don't want to tell me when you were created, you don't have to be rude about it. Do what every other sentient being does and make up some numbers."
		when "OnUserSpecies";
			"%(b0)It seems asking what sort of being I'm communicating with has hit a nerve. Allow me to assure you, it makes no difference to me what you are. I will think no less of you than I do now."
		when "OnUserPlanet";
			"%(b0)Wow.\w4 Okay.\w4 Look, if you're going to drag part of my consciousness all the way out here to speak to you, the least you could do is tell me where, exactly, I've been dragged. None of this walking away in the middle of giving me my well deserved answers."
	}
	--
	"\x\c\![raise,OnUserInputCancel,%(_argv)]" //Start input box
}
OnBoot_Dialogue : nonoverlap
{
	"%(b0)Greetings, summoner."
	"%(b0)Hello once more, %(username) of %(userplanet)."
	"%(b0)It is I, the Squid."
	"%(b0)I am here to be entertained."
	"%(b0)Pardon my lack of overwhelming radiance. I am currently linking with several of my mindbonded, and thus I can only be astoundingly radiant for you."
	"%(b0)Yo."
	"%(b0)It's about time you showed up. There's only so long you can expect me to watch you not paying attention to me."
	"%(b0)It's an especially squidly day, isn't it?"
	"%(b0)%(username), %(username), %(username). I was wondering when you would decide to show your face."

}

//------------------------------------------------------------------------------
//  Closing sequences
//------------------------------------------------------------------------------
OnClose //Forced
{
	"%(b0)Fine. I needed to go buy %(deco) anyway."
	"%(b0)Some people say goodbye before they kick you out.\n\nNot you, apparently."
	"%(b0)GOODBYE, summoner.\w8\w8\n\nSee how I did that?\w8 Bid you farewell before I left?\w8\n\nTry it sometime."
	--
	"\w8\w8\-"
}

OnClose_Own //Through menu
{
	"%(b0)It's been an interesting visit. Take care of that weak meat body of yours."
	"%(b0)So long for now, summoner. Think fondly of our time together and I shall hear your affection across the channels of space-time."
	"%(b0)Ah, to be parted. To be alone! To know neither touch nor kind word! To have the chill of space once occupied suddenly vacant.\n\nI\w8\w8\w8 forgot where I was going with that.\w8\w8 See you later, %(username)."
	--
	"\w8\w8\-"
}


//------------------------------------------------------------------------------
// Shiori functions
//------------------------------------------------------------------------------

//Parser
OnTranslate {
	_talk = reference0
	_talk = EVAL('"' + REPLACE(_talk,'"','" + CHR(0x22) + "') + '"')
	//_talk = "\_?" + _talk //bugfix
	TOSTR(_talk)
}
OnAnchorSelect {
	if "http://" _in_ reference0 || "https://" _in_ reference0;
		"\j[%(reference0)]"
	elseif "OnTimer" _in_ reference0;
		EVAL(reference0)
	elseif reference0 == "Script:\e";
		"\e"
}

//Input functions
OnUserInputCancel //can be used to ensure input; reference0 = Function the box calls
{
	_txt = ""; _itext = ""
	case reference0 {
		when "OnUsernameSet"; _itext = ",Your name"
		when "OnUserBirthday"; _itext = ",Day%(CHR(44))Month"
		when "OnUserSpecies"; _itext = ",Your species"
		when "OnUserPlanet"; _itext = ",Your planet"
	}

	if reference0 == "OnTimer_Save"; _txt = OnTimer
	else; _txt = "\![open,inputbox," + reference0 + ",-1" + _itext + "]"
	_txt
}
OnKeyPress {
	//reference0 = key pressed. Only on ghost focus.
}


//Shiori time Functions
OnSecondChange {
	OnTimer_Count
}
OnMinuteChange {
	//if 5-10 min after boot
	//Birthday checks
	if !birthdaycheck[0] && DAY('%(squidbirthday[0],%(squidbirthday[1]))') && year > squidbirthday[2] {
		if (ghostupmin > 5 && RND(80)) && CheckInputs == ""; OnBirthday_Squid
	}
	if DAY(userbirthday) && !birthdaycheck[1] && (ghostupmin > 5 && RND(80)) && CheckInputs == ""; OnBirthday_User
}
OnHourTimeSignal {
	//Notify only
}


//Updates
OnUpdateCheckComplete {
	//reference0 = amount of files(?); none = no new files
	if reference0 != "none"; updatetrue = 1
	else; updatetrue = 0
}
OnUpdateCheckFailure {}
OnUpdateBegin {}
OnUpdateReady {
	//reference0 = new files
	"%(b0)There is a wave of change rippling through the layers of the multiverse, altering all those that succumb to it. We, too, should allow ourselves to be 'updated'."
}
OnUpdate.OnDownloadBegin {
	//reference0 = filename
}
OnUpdateComplete {
	//reference0 = amount of files(?); none = no updated
	updatetrue = 0
	--
	"%(b0)Your update has been completed. How are you feeling? Headache? Nausea? Crippling fear of a potential porpoise behind you?\n\n\w8I may be projecting that last one."
}
OnUpdateFailure {
	//reference0 = error reason
	"%(b0)There seems to have been a problem with your update. You'll not reach transcendence if you don't figure out where the problem is\w4, and I do not particularly care to help.\n\nBest of luck."
}

//Installs
OnInstallBegin {
	"%(b0)Interested in contributing to the collective mind-well, are you? Wonderful! Allow me a moment to intigrate this new knowledge."
}
OnInstallComplete {
	"%(b0)Your contribution to my brain nourishment has passed all evaluation. Let me know if you'd like to add anything else."
}
OnInstallFailure {
	"%(b0)There's a problem with your files that you might want to figure out.\w8\w8\n\nYour.\w4.\w4.\w4 brain files. That you sent.\w4.\w4.\w4 with your brain.\w8\w8 Because I'm telepathic.\w8\w8 Not a program."
}
OnInstallRefuse {
	"%(b0)This is not for me, I have no interest in it, and if you keep trying to give it to me, I will eat it and you won't get it back. Try elsewhere."
}

//Deinstalling
OnVanishSelecting {}
OnVanishSelected {
	"%(b0).\w8.\w8.\w8\n\nYou shall regret this.\w8\w8\w8"
}

//Low battery refernce0 = percentage
OnBatteryLow {
	"%(b0)It appears that the connection between us is weakening. You may want to check your energy reserves, lest the strain of being near me while weak cause you to implode."
}
OnBatteryCritical {
	"%(b0)Death comes for all trapped within the flow of time, and right now, it's coming for you and your transdimensional communications device."
}

//------------------------------------------------------------------------------
// Functions related to ssp menu/aya
//------------------------------------------------------------------------------
On_homeurl { }
On_username { username }
On_recommendrootbutton.caption { "Reference Sites" }
On_sakura.recommendsites
{
	"SSP/
		%ASC(1)http://ssp.shillest.net/%ASC(2)/
	Ukagaka Dream Team Tumblr/
		%ASC(1)https://ukagakadreamteam.tumblr.com/%ASC(2)"
}
On_portalrootbutton.caption { "Developers sites" }
On_sakura.portalsites
{

}
On_updatebutton.caption { "Update" }
On_vanishbutton.caption { "Uninstall" }
On_vanishbutton.visible
{
 	if deletetrue == 1; "1"
	else; "0"
}
On_headlinesenserootbutton.caption { "News" }
On_ghostrootbutton.caption { "Change Ghost" }
On_callghostrootbutton.caption { "Call Ghost" }
On_otherghostname
{
	ghostexlist = reference
	ghostexcount = ARRAYSIZE(ghostexlist)
}
On_shellrootbutton.caption { "Change shell" }
On_dressuprootbutton.caption { "Clothes" }
On_readmebutton.caption {"Information" }
On_quitbutton.caption{ "Quit" }

On_version { GETSETTING(AYAINFO_VERSION) }
On_craftman { "umeici" }
On_craftmanw { "?ëŸ–" }
On_name { "AYA" }

On_uniqueid { uniqueid = reference0 }

On_hwnd
{
	sakurahwnd    = reference0[0]
	kerohwnd      = reference0[1]
	sakurablnhwnd = reference1[0]
	keroblnhwnd   = reference1[1]
}

On_\person
{
 	person
}